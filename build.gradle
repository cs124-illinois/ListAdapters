// Top-level build file where you can add configuration options common to all sub-projects/modules.

apply plugin: "com.github.ben-manes.versions"

buildscript {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:8.1.2"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.48.0"
    }
}

allprojects {
    group("com.github.cs124-illinois.ListAdapters")
    version("2023.10.0")
    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf { isNonStable(it.candidate.version) }
}
